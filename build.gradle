apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

group='com.xconns.peerdevicenet'
archivesBaseName='peerdevicenet-router'
version='1.1.7'

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
}

android {
    compileSdkVersion 19
    buildToolsVersion "23.0.1"

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }

        // Move the tests to tests/java, tests/res, etc...
        instrumentTest.setRoot('tests')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }
}

    task "androidJavadocsRelease"(type: Javadoc, dependsOn: "assembleRelease") {
        source = android.sourceSets.main.java.srcDirs
        //source += files(android.sourceSets.main.generatedFiles)
        ext.androidJar = files(android.getBootClasspath())
        classpath += /*files(javaCompile.classpath.files) +*/ ext.androidJar
        //classifier = 'javadoc'
        exclude '**/R.html', '**/R.*.html', '**/index.html'
        exclude '**/*.aidl','**/package-info.java'
    }

    task "androidJavadocsJarRelease"(type: Jar, dependsOn: "androidJavadocsRelease") {
        classifier = 'javadoc'
        baseName = archivesBaseName
        from tasks."androidJavadocsRelease".destinationDir
    }

task androidSourcesJar(type: Jar, dependsOn: assembleRelease) {
    classifier = 'sources'
    baseName = archivesBaseName
    from android.sourceSets.main.java.srcDirs
}


  artifacts {
    archives androidSourcesJar
    archives androidJavadocsJarRelease
  }

/* signing */
  signing {
      sign configurations.archives
  }

uploadArchives {
    repositories {
        mavenDeployer {
            //repository(url: "file:///tmp/myrepo")
            
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
               authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
               authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            
            pom.project {
                name archivesBaseName
                packaging 'aar'
                // optionally artifactId can be defined here 
                description 'android aar package for PeerDeviceNet runtime.'
                url 'https://peerdevicenet.github.io'

                licenses {
                    license {
                       name 'The Apache License, Version 2.0'
                       url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                   developer {
                      name 'Yigong Liu'
                      email 'peerdevicenet@gmail.com'
                   }
                }

                scm {
                   connection 'scm:git:git@github.com:peerdevicenet/Router.git'
                   developerConnection 'scm:git:git@github.com:peerdevicenet/Router.git'
                   url 'git@github.com:peerdevicenet/Router.git'
                }
            }
        }
    }
}

uploadArchives.dependsOn(build, androidSourcesJar, androidJavadocsJarRelease)

